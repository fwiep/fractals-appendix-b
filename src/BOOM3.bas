10 REM ***BOOMSTRUCTUUR IN DRIETALLIG TALSTELSEL***
20 REM ***NAAM:BOOM3 (C) 1987 HANS LAUWERIER***
30 SCREEN 3: CLS : PI = 3.141593
40 WINDOW (-1.2, -.9)-(1.2, .9)
50 P = 5: DIM T(P): REM ***KEUZE ORDE***
60 A = .45: REM ***KEUZE REDUCTIEFACTOR***
70 FOR M = 0 TO P
80 FOR N = 0 TO 3 ^ M - 1
90 REM ***DRIETALLIGE NOTATIE VAN N***
100 N1 = N: FOR L = 1 TO M
110 T(L) = N1 MOD 3: N1 = N1 \ 3: NEXT L
120 X = 0: Y = 0
130 FOR K = 1 TO M
140 F = (2 * T(K) * PI) / 3
150 X = X + COS(F) * A ^ K: Y = Y + SIN(F) * A ^ K
160 NEXT K
170 LINE (X, Y)-(X + A ^ K, Y)
180 LINE (X, Y)-(X - .5 * A ^ K, Y + SQR(3) / 2 * A ^ K)
190 LINE (X, Y)-(X - .5 * A ^ K, Y - SQR(3) / 2 * A ^ K)
200 NEXT N: NEXT M: END

