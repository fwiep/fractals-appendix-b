10 REM ***KRONKELLIJN , TALSTELSELMETHODE , KOCHKRUIS***
20 REM ***NAAM:KRONKELT (C) 1987 HANS LAUWERIER***
30 SCREEN 3: CLS : PI = 3.141593
40 WINDOW (-2.8, -2.1)-(2.8, 2.1)
50 INPUT P: DIM T(P)
60 U = 4: DIM A(U), B(U): REM ***AANTAL ELEMENTEN BEGINLIJN***
70 REM ***BEGINLIJN ,VECTOREN IN LENGTE EN RICHTING IN GRADEN***
80 DATA 2,0,2,90,2,180,2,-90
90 X0 = -1: Y0 = -1: REM ***COORDINATEN BEGINHOEKPUNT***
100 FOR I = 0 TO U - 1: READ A(I), B(I): B(I) = B(I) * PI / 180: NEXT I
110 V = 4: DIM L(V), F(V): REM ***AANTAL ELEMENTEN MOTIEF***
120 REM ***MOTIEF ,VECTOREN IN LENGTE EN RICHTING IN GRADEN***
130 DATA 1,0,1,60,1,-60,1,0
140 FOR I = 0 TO V - 1: READ L(I), F(I): F(I) = F(I) * PI / 180: NEXT I
150 S = 0: FOR I = 0 TO V - 1: S = S + L(I) * COS(F(I)): NEXT I
160 FOR I = 0 TO V - 1: L(I) = L(I) / S: NEXT I
170 PSET (X0, Y0): FOR K = 0 TO U - 1
180 FOR N = 0 TO V ^ P - 1: REM ***HOOFDCYCLUS***
190 M = N: FOR J = 0 TO P - 1
200 T(J) = M MOD V: M = M \ V: NEXT J
210 L = A(K): F = B(K)
220 FOR J = 0 TO P - 1
230 L = L * L(T(J)): F = F + F(T(J)): NEXT J
240 X = L * COS(F): Y = L * SIN(F)
250 LINE -STEP(X, Y)
260 NEXT N: NEXT K: END

