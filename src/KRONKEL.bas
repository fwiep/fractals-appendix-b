10 REM ***FRACTAL KROMME MET BASISLIJN EN MOTIEF , KOCH KRUIS***
20 REM ***NAAM:KRONKEL (C) 1987 HANS LAUWERIER***
30 SCREEN 3: CLS : PI = 3.141593
40 DIM X(4096), Y(4096)
50 WINDOW (-2.4, -1.8)-(2.4, 1.8)
60 U = 4: DIM A(U), B(U): REM ***AANTAL ELEMENTEN BASISLIJN***
70 V = 4: DIM C(V), D(V): REM ***AANTAL ELEMENTEN MODEL***
80 DATA 1,1,-1,1,-1,-1,1,-1,1,1 : REM ***DATA BEGINLIJN***
90 DATA .333,0,.5,.2887,.667,0 : REM ***DATA MODEL***
100 INPUT P: REM ***KEUZE ORDE***
110 FOR I = 0 TO U: READ A(I), B(I): NEXT I
120 FOR I = 1 TO V - 1: READ C(I), D(I): NEXT I
130 REM ***BEREKENING COORDINATEN HOEKPUNTEN KRONKELLIJN***
140 C(0) = 0: D(0) = 0: X(0) = 0: Y(0) = 0: X(V ^ P) = 1: Y(V ^ P) = 0
150 FOR I = 0 TO P - 1
160 FOR J = 0 TO V ^ P - 1 STEP V ^ (P - I)
170 M1 = J + V ^ (P - I): X1 = X(M1) - X(J): Y1 = Y(M1) - Y(J)
180 FOR K = 1 TO V - 1
190 M2 = J + K * V ^ (P - I - 1)
200 X(M2) = X1 * C(K) - Y1 * D(K) + X(J): Y(M2) = Y1 * C(K) + X1 * D(K) + Y(J)
210 NEXT K: NEXT J: NEXT I
220 REM ***TEKENWERK , VOOR ELKE ZIJDE M VAN DE BASISLIJN WORDT DE KRONKELLIJN AFGEBEELD***
230 PSET (A(0), B(0))
240 FOR M = 0 TO U - 1: A = A(M + 1) - A(M)
250 B = B(M + 1) - B(M): FOR N = 0 TO V ^ P
260 X = A * X(N) - B * Y(N) + A(M): Y = B * X(N) + A * Y(N) + B(M)
270 LINE -(X, Y)
280 NEXT N: NEXT M: END

