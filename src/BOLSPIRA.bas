10 REM ***BOLSPIRAAL***
20 REM ***NAAM:BOLSPIRA (C) 1987 HANS LAUWERIER***
30 SCREEN 3: CLS : PI = 3.141593
40 WINDOW (-2, -1.5)-(2, 1.5)
50 A = .2: REM ***KEUZE SPIRAALCONSTANTE***
60 C = .9: REM ***KEUZE HELLING PROJECTLIEVLAK***
70 P = 1 / SQR(2): Q = P * SQR(1 - C * C): REM ***PROJECTIEGETALLEN***
80 FOR N = -500 TO 500
90 S = N * PI / 50: T = ATN(A * S)
100 X = COS(S) * COS(T): Y = SIN(S) * COS(T): Z = -SIN(T)
110 U = P * (Y - X): V = C * Z - Q * (X + Y)
120 IF N = -500 THEN PSET (U, V) ELSE LINE -(U, V)
130 NEXT N: END

